import "main.del";

// will have weird alignment for aspect ratios other than 16:9
Vector FacingTextLocation(in Number X, in Number Y): 
Update(EyePosition(local) + 100 * 
          (X * WorldVectorOf(Right(), local, LocalVector.Rotation) + // X
          (Y // Y
          - 0.200) * DirectionFromAngles(HorizontalAngleFromDirection(Facing(local)), VerticalAngleFromDirection(Facing(local)) - 90) +
          3 * Facing(local)));

rule: "[Main] :: General HUD Text"
{
    // time
    CreateHudText(All(), null, <"<0>:<1><2>", Floor(MatchTime() / 60 % 60), 
                                MatchTime() % 60 < 10 ? "0" : "", 
                                Floor(MatchTime() % 60)>,
    " \n\n\n\n\n\n\n\n", Location.Top, 0.1);
}

Color CanPlayerPickupBuffColor(Number index): 
    local.playerHasBuff && 
        (local.currentBuffIn != Once(index) || local.infAmmo)
        ? Color.Gray : buffColors[Once(index)];

rule: "[Main] :: Pickup HUD Text and Effects : Welcome to hell"
if (validMap)
if (S_ENABLE_BUFFS)
{
    for (Number i = 0; i < buffNames.Length; i++)
    {
        if (S_SPAWN_BUFF[i])
        {
            // ammo
            CreateHudText(
                VisibleTo:        local.buffEnabled[Once(i)] ? local : null,
                Header:           null,
                Subheader:        null,
                Text:             <"<0> <1> Ammo: <2>",
                                        buffIcons[Once(i)], 
                                        buffNames[Once(i)], 
                                        !local.infAmmo ? 
                                            !local.showAmmoAsPercentage ? 
                                                Ceil(local.heldBuffAmmo[Once(i)]) 
                                            : <"<0>%", Ceil(Ceil(local.heldBuffAmmo[Once(i)]) / S_BUFF_AMMO[Once(i)] * 100)>
                                        : "âˆž">, 
                Location:         Location.Top, 
                SortOrder:        1, 
                TextColor:        buffColors[Once(i)], 
                Reevaluation:     HudTextRev.VisibleToSortOrderStringAndColor);

            // complete orb effect
            CreateEffect(
                VisibleTo:        buffCooldown[Once(i)] == 0 ? All() : null, 
                Type:             Effect.BadAura, 
                Color:            CanPlayerPickupBuffColor(i), 
                Position:         buffPos[Once(i)], 
                Radius:           0.45, 
                Reevaluation:     EffectRev.VisibleToAndColor);

            // complete text
            CreateInWorldText(
                VisibleTo:        buffCooldown[Once(i)] == 0 ? All() : null, 
                Header:           " \n\n\n" + buffIcons[Once(i)], 
                Position:         buffPos[Once(i)] + Vector(0, 0.085, 0),
                Scale:            2.1, 
                Clipping:         Clipping.ClipAgainstSurfaces, 
                Reevaluation:     InworldTextRev.VisibleToAndPosition);

            // completed/in-progress good aura effect
            CreateEffect(
                VisibleTo:        All(), 
                Type:             Effect.GoodAura, 
                Color:            CanPlayerPickupBuffColor(i),
                Position:         buffPos[Once(i)], 
                Radius:           buffPickupEffectSize[Once(i)] * 0.0045,
                Reevaluation:     EffectRev.VisibleToPositionRadiusAndColor);

            // completed/in-progress sparkle effect
            CreateEffect(
                VisibleTo:        All(), 
                Type:             Effect.Sparkles, 
                Color:            CanPlayerPickupBuffColor(i),
                Position:         buffPos[Once(i)], 
                Radius:           buffPickupEffectSize[Once(i)] * 0.0045,
                Reevaluation:     EffectRev.VisibleToPositionRadiusAndColor);

            // in progress sphere effect
            CreateEffect(
                VisibleTo:        buffCooldown[Once(i)] != 0 ? All() : null, 
                Type:             Effect.Sphere, 
                Color:            CanPlayerPickupBuffColor(i), 
                Position:         buffPos[Once(i)], 
                Radius:           buffPickupEffectSize[Once(i)] * 0.0045,
                Reevaluation:     EffectRev.VisibleToPositionRadiusAndColor);

            // in progress progress bar
            CreateProgressBarInWorldText(
                VisibleTo:        buffCooldown[Once(i)] != 0 ? All() : null, 
                Value:            buffPickupEffectSize[Once(i)], 
                Text:             null, 
                Position:         buffPos[Once(i)] + Vector(0, buffPickupEffectSize[Once(i)] * 0.0045, 0),
                Scale:            0, 
                Clipping:         Clipping.ClipAgainstSurfaces, 
                ProgressBarColor: buffColors[Once(i)], 
                TextColor:        Color.White, 
                Reevaluation:     ProgressBarWorldEvaluation.VisibleToPositionValuesAndColor);
        }
    }

    // buckshot
    if (S_SPAWN_BUFF[2])
    {
        // alt fire
        CreateHudText(local.buffEnabled[2] ? local : null, null, 
        <"<0>: Charge shots", InputBindingString(Button.Reload)>, null,
        Location.Top, 1.1, SubheaderColor: buffColors[2]);

        // charge
        CreateHudText(local.buffEnabled[2] ? local : null, null,  
        <" Charges: <0>", local.chargeStr>, null,
        Location.Top, 1.2, SubheaderColor: buffColors[2]);
    }

    // prism
    if (S_SPAWN_BUFF[3])
    {
        // alt fire
        CreateHudText(local.buffEnabled[3] ? local : null, null,
        <"<0>: Alternate fire", InputBindingString(Button.Reload)>, null,
        Location.Top, 1.1, SubheaderColor: buffColors[3]);
    }

    // electro
    if (S_SPAWN_BUFF[5])
    {
        // alt fire
        CreateHudText(local.buffEnabled[5] ? local : null, null, 
        <"<0>: Alternate fire", InputBindingString(Button.Reload)>, null,
        Location.Top, 1.1, SubheaderColor: buffColors[5]);

        // armed
        CreateHudText(local.totalProjAirtime != 0 ? local : null, null,  
        AltFont(<"Detonation in: <0>", local.projHitSurface ? Max(S_ELECTRO_PROJ_DELAY - local.airtime, 0.001) : Max(FINAL_PROJ_DELAY - local.totalProjAirtime, 0.001)>), null,
        Location.Top, 1.2, SubheaderColor: local.projHitSurface || local.totalProjAirtime > 1 ? Color.Red : buffColors[5], Reevaluation: HudTextRev.VisibleToSortOrderStringAndColor);
    }

    // spark
    if (S_SPAWN_BUFF[6])
    {
        // alt fire
        CreateHudText(local.buffEnabled[6] ? local : null, null, 
        <"<0>: Alternate fire", InputBindingString(Button.Reload)>, null,
        Location.Top, 1.1, SubheaderColor: buffColors[6]);
    }

    // volt
    if (S_SPAWN_BUFF[9])
    {
        // temperature
        CreateProgressBarHudText(local.buffEnabled[9] ? local : null, 
        local.voltTemp * 100, !local.overheated ? <"<0>%", Floor(Clamp(local.voltTemp * 100 + (local.voltTemp * (Time() + 1000)) % 8, 0, 99))> : "Overheat",
        Location.Top, 1.2, 
        CustomColor(
            Lerp(255, 255, local.voltTemp), // r
            Lerp(100, 0, local.voltTemp),  // g
            Lerp(0, 0, local.voltTemp),  // b
            255), 
        CustomColor(
            Lerp(255, 255, local.voltTemp), // r
            Lerp(100, 0, local.voltTemp),  // g
            Lerp(0, 0, local.voltTemp),  // b
            255), 
        ProgressBarHudEvaluation.VisibleToValuesAndColor);
    }

    // mutators
    if (S_ENABLE_MUTATORS)
    {
        // mutator info
        CreateHudText(
            isMutatorInWorld || mutatorUser != null ? RemoveFromArray(All(), mutatorUser) : null, 
            mutatorIcon, 
            mutatorStr, 
            mutatorProgressBar != 100 && mutatorCooldown <= S_M_CREATION 
                ? <"<0>%", Floor(mutatorProgressBar)> 
                : !IsTrueForAny(All(), Curr().playerHasMutator) 
                    ? IconString(Icon.Checkmark)
                    : <"In-use by <0>", mutatorUser>,
            Location.Left, 
            24, 
            mutatorColors[0], 
            mutatorColors[0], 
            Reevaluation: HudTextRev.VisibleToSortOrderStringAndColor);

        // icon
        CreateInWorldText(local.playerHasMutator ? local : null, 
        mutatorIcon, 
        FacingTextLocation(-0.65, -1.028), 
        1.2, 
        Clipping.DoNotClip, InworldTextRev.VisibleToPositionStringAndColor, 
        mutatorColors[0]);

        // text
        CreateInWorldText(local.playerHasMutator ? local : null, 
        mutatorStr,
        FacingTextLocation(0, -0.93), 
        1.75, 
        Clipping.DoNotClip, InworldTextRev.VisibleToPositionStringAndColor, 
        mutatorColors[0]);

        // progress bar
        CreateProgressBarInWorldText(local.playerHasMutator ? local : null, local.mutatorNormalTimeLeft, 
        null,
        FacingTextLocation(0, -1.235), 
        0.65, Clipping.DoNotClip, 
        mutatorColors[1], 
        null,
        ProgressBarWorldEvaluation.VisibleToPositionValuesAndColor);

        // ability text
        CreateInWorldText(local.playerHasMutator ? local : null, 
        StringReplace(mutatorDescs[chosenMutator], "*", InputBindingString(local.abilityButton)),
        FacingTextLocation(0, -1.125), 
        1, 
        Clipping.DoNotClip, InworldTextRev.VisibleToPositionStringAndColor, 
        mutatorColors[0]);

        // mutator spawning
        CreateProgressBarInWorldText(isMutatorInWorld && mutatorProgressBar != 100 ? All() : null, mutatorProgressBar, null, mutatorPos + Vector(0, mutatorProgressBar * 0.0045, 0), 0, Clipping.DoNotClip, mutatorColors[0], null, ProgressBarWorldEvaluation.VisibleToPositionValuesAndColor);
        CreateEffect(isMutatorInWorld ? All() : null, Effect.BadAura, mutatorColors[1], mutatorPos, mutatorProgressBar * 0.0045, EffectRev.VisibleToPositionRadiusAndColor);
        CreateEffect(isMutatorInWorld ? All() : null, Effect.GoodAura, mutatorColors[0], mutatorPos, mutatorProgressBar * 0.0045, EffectRev.VisibleToPositionRadiusAndColor);
        CreateEffect(isMutatorInWorld ? All() : null, Effect.Sparkles, mutatorColors[0], mutatorPos, mutatorProgressBar * 0.0045, EffectRev.VisibleToPositionRadiusAndColor);

        // mutator spawned
        CreateInWorldText(isMutatorInWorld && mutatorProgressBar == 100 ? All() : null, mutatorIcon, mutatorPos + Vector(0, 0.65, 0), 2, Clipping.DoNotClip, InworldTextRev.VisibleToPositionAndString);
    }
}