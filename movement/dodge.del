import "../main.del";

globalvar Number S_DODGE_FORCE;
globalvar Boolean S_DODGE_ENABLED;
globalvar Number S_DODGE_FUEL_COST;
playervar Number dodgeFuelCost;
playervar Boolean isDodging;
playervar Vector throttle;
playervar Number dodgeForce;

disabled rule: "---------------- Dodge ----------------"{}

rule: "[Dodge] :: Init"
Event.OnPlayerJoin
{
    dodgeForce = S_DODGE_FORCE;
    dodgeFuelCost = S_DODGE_FUEL_COST;
}

Number y: 0.6;
rule: "[Dodge] :: Action"
Event.OngoingPlayer
if (S_DODGE_ENABLED)
if (fuel >= dodgeFuelCost)
if (IsAlive())
if (IsButtonHeld(player, Button.Ability1))
{
    isDodging = true;

    fuelDepletionFlashStartTime = Time();
    fuelDepletionFlashTriggered = true;

    if (!infFuel)
        fuel -= dodgeFuelCost;

    // hacky fix for controller users
    throttle = Vector(Near(ThrottleOf().X), y, Near(ThrottleOf().Z));

    if (throttle == Vector(0, y, 0))
    {
        throttle = Vector(0, y, 0);
        dodgeForce /= 2;
    }

    ApplyImpulse(player, throttle, dodgeForce, Relative.ToPlayer, ContraryMotion.Cancel);

    if (equippedAbility != Ability.Stimpack || !usingAbility)
        dodgeForce = S_DODGE_FORCE;
    else
        dodgeForce = S_DODGE_FORCE * 1.3;

    PlayEffect(All(), PlayEffect.BadExplosion, Color.Orange, PositionOf(), 1);
    PlayEffect(All(), PlayEffect.RingExplosion, Color.Orange, PositionOf(), 2);
    PlayEffect(All(), PlayEffect.DvaMicroMissilesExplosionSound, Color.White, PositionOf(), 100);

    Wait(0.15); // small cooldown
}

rule: "[Dodge] :: Is Not Dodging"
Event.OngoingPlayer
if (Update(IsOnGround() || AltitudeOf() < 0.01))
{
    isDodging = false;
    fuelDepletionFlashTriggered = false;
}